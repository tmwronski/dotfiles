# Maintainer: Tomasz Wro≈Ñski <tm.wronski@gmail.com>
pkgname=dmenu
pkgver=4.9
pkgrel=1
pkgdesc="A generic menu for X."
arch=('x86_64')
url="https://tools.suckless.org/dmenu/"
license=('MIT')
depends=('sh' 'libxinerama' 'libxft')
options=(zipman)
source=("https://dl.suckless.org/tools/$pkgname-$pkgver.tar.gz" "config.h")
sha256sums=('b3971f4f354476a37b2afb498693649009b201550b0c7c88e866af8132b64945' 'SKIP')

prepare() {
    cp $srcdir/config.h $srcdir/$pkgname-$pkgver/config.h
}

build() {
    cd "$srcdir/$pkgname-$pkgver"
    make X11INC=/usr/include/X11 X11LIB=/usr/lib/X11
}

package() {
    cd "$srcdir/$pkgname-$pkgver"
    make PREFIX=/usr DESTDIR="$pkgdir" install
    install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
    install -Dm644 README "$pkgdir/usr/share/doc/$pkgname/README"
}

