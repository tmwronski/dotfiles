# Maintainer: Tomasz Wro≈Ñski <tm.wronski@gmail.com>
pkgname=dwm
pkgver=6.2
pkgrel=1
pkgdesc="A dynamic window manager for X."
arch=('x86_64')
url="https://dwm.suckless.org"
license=('MIT')
depends=('libx11' 'libxinerama' 'libxft' 'freetype2' 'noto-fonts' 'noto-fonts-extra' 'noto-fonts-emoji')
optdepends=('st: a simple terminal emulator for X' 'dmenu: a generic menu for X')
options=(zipman)
source=("https://dl.suckless.org/dwm/$pkgname-$pkgver.tar.gz" "config.h")
sha256sums=('97902e2e007aaeaa3c6e3bed1f81785b817b7413947f1db1d3b62b8da4cd110e' 'SKIP')

prepare() {
    # user is supposed to maintain config.h him/herself
    cp $srcdir/config.h $srcdir/$pkgname-$pkgver/config.h
}

build() {
    cd "$srcdir/$pkgname-$pkgver"
    make X11INC=/usr/include/X11 X11LIB=/usr/lib/X11 FREETYPEINC=/usr/include/freetype2
}

package() {
    cd "$srcdir/$pkgname-$pkgver"
    make PREFIX=/usr DESTDIR="$pkgdir" install
    install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
    install -Dm644 README "$pkgdir/usr/share/doc/$pkgname/README"
}

